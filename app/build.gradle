def flavor = ""

apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    dexOptions {
        javaMaxHeapSize "2g"
    }
    defaultConfig {
        applicationId "study.pmoreira.builditbigger"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        buildTypes.each {
            it.buildConfigField 'String', 'JOKE_API_ROOT_URL', jokeApitRootUrl
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            if (variant.getName() == "freeDebug" || variant.getName() == "freeRelease") {
                variant.buildConfigField 'String', 'TEST_DEVICE_ID', testDeviceId
            }
        }
    }
    productFlavors {
        free {
            applicationId "study.pmoreira.builditbigger.free"
            flavor = "free"
        }
        paid {
            applicationId "study.pmoreira.builditbigger.paid"
            flavor = "paid"
        }
    }
}

ext {
    butterKnifeVersion = '8.5.1'
    supportLibVersion = '25.3.1'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    testCompile 'junit:junit:4.12'

    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"

    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'com.google.api-client:google-api-client-android:1.22.0'

    compile project(":jokeactivity")
    compile project(path: ':backend', configuration: 'android-endpoints')

    freeCompile 'com.google.firebase:firebase-core:10.2.4'
    freeCompile 'com.google.firebase:firebase-ads:10.2.4'
}


task startServer(dependsOn: ":backend:appengineRun") {
    project(":backend").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }
}

task internalTests {
    dependsOn = [":jokeactivity:test", ":jokeactivity:connectedAndroidTest", ":app:test", ":app:connectedAndroidTest"]
    finalizedBy ":backend:appengineStop"
}

task runTests(dependsOn: ":app:startServer") {
    finalizedBy ":app:internalTests"
}

if (flavor == "free") {
    apply plugin: 'com.google.gms.google-services'
}